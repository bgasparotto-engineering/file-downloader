plugins {
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id "com.commercehub.gradle.plugin.avro" version "0.9.1"

    id 'java'
    id "jacoco"
    id "org.sonarqube" version "3.0"

    id 'idea'
}

group = 'com.bgasparotto'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    developmentOnly
    runtimeClasspath {
        extendsFrom developmentOnly
    }
    compileOnly {
        extendsFrom annotationProcessor
    }
    integrationTestImplementation {
        extendsFrom testImplementation
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://packages.confluent.io/maven/'
    }
}

sourceSets {
    integrationTest {
        java.srcDir "$projectDir/src/it/java"
        resources.srcDir "$projectDir/src/it/resources"
        compileClasspath += sourceSets.main.output + test.output
        runtimeClasspath += sourceSets.main.output + test.output
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    implementation 'org.springframework.kafka:spring-kafka'
    implementation 'org.apache.avro:avro:1.9.1'
    implementation 'io.confluent:kafka-avro-serializer:5.3.1'
    implementation 'io.confluent:kafka-streams-avro-serde:5.3.1'

    implementation 'org.apache.hadoop:hadoop-client:3.1.3'
    implementation 'org.apache.commons:commons-lang3:3.9'
    implementation 'commons-io:commons-io:2.6'

    compileOnly 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql'
    annotationProcessor 'org.projectlombok:lombok'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
    testImplementation 'com.bgasparotto:spring-kafka-avro-test:1.0.3'
}

test {
    useJUnitPlatform()
}

task integrationTest(type: Test) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

sonarqube {
    properties {
        property "sonar.projectKey", "bgasparotto_file-downloader"
        property "sonar.organization", "bgasparotto-github"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}
